plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'checkstyle'
	id 'pmd'
}

group = 'com.deacero.beta'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Cambiamos a WebFlux en lugar de Web MVC
	implementation 'org.springframework.boot:spring-boot-starter-webflux:3.3.1'
	// Cambiamos a la versi√≥n reactiva de SpringDoc OpenAPI
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.1.0'
	// R2DBC para PostgreSQL
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc:3.3.1'
	implementation 'org.postgresql:r2dbc-postgresql:1.0.5.RELEASE' // Conector R2DBC para PostgreSQL
	implementation 'org.springframework.boot:spring-boot-starter-actuator:3.3.1'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'org.threeten:threetenbp:1.6.9'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	// Dependencias para pruebas y bases de datos
	implementation 'io.r2dbc:r2dbc-h2'
	testImplementation group: 'com.h2database', name: 'h2', version: '2.3.230'
	runtimeOnly 'com.h2database:h2:2.3.230'

	compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.34'
	annotationProcessor 'org.projectlombok:lombok:1.18.34'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

spotless {
	java {
		target 'src/**/*.java'
		googleJavaFormat('1.15.0')
		removeUnusedImports()
		indentWithSpaces()
		endWithNewline()
	}
}

checkstyle {
	toolVersion = '10.10.0'
	config = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml')
}

pmd {
	consoleOutput = true
	toolVersion = "7.0.0"
	rulesMinimumPriority = 5
	ruleSetFiles = files("config/pmd/ruleset.xml")
}

tasks.withType(Checkstyle).configureEach {
	reports {
		html.required.set(true)
		xml.required.set(false)
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootJar') {
	enabled = true
}
